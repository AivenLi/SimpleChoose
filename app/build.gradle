plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

android {
    compileSdk rootProject.ext.android.compileSdk

    signingConfigs {
        debug {
            storeFile file('simplechoose.jks')
            storePassword '3345678990'
            keyAlias 'simple'
            keyPassword '3345678990'
        }
        release {
            storeFile file('simplechoose.jks')
            storePassword '3345678990'
            keyAlias 'simple'
            keyPassword '3345678990'
        }
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    defaultConfig {
        applicationId "com.aiven.simplechoose"
        minSdk rootProject.ext.android.minSdk
        targetSdk rootProject.ext.android.targetSdk
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true  // 移除无用的resource文件
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->
        def baseName = "SimpleChoose"
        def filename
        def createDate = new Date().format("yyyy-MM-dd_hh_mm_ss", TimeZone.getTimeZone("GMT+08:00"))
        if (variant.buildType.name == "debug") {
            filename = "${baseName}_Debug_${variant.versionName}_${variant.versionCode}_${createDate}.apk"
        } else {
            filename = "${baseName}_Release_${variant.versionName}_${variant.versionCode}_${createDate}.apk"
        }
        variant.outputs.each { output ->
            output.outputFileName = filename
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    viewBinding {
        enabled = true
    }
}

dependencies {

    implementation rootProject.ext.dependencies.androidKtx
    implementation rootProject.ext.dependencies.androidCompat
    implementation rootProject.ext.dependencies.androidMeterial
    implementation rootProject.ext.dependencies.androidxConstraintLayout

    // 图片加载框架
    implementation rootProject.ext.dependencies.glide
    // 配合Glide使用
    annotationProcessor rootProject.ext.dependencies.glideCompiler

    // Retrofit
    implementation rootProject.ext.dependencies.retrofit
    // Retrofit 适配器
    implementation rootProject.ext.dependencies.retrofitAdapterRxJava3
    // Retrofit 转换器
    implementation rootProject.ext.dependencies.retrofitConverterGson
    // RxAndroid
    implementation rootProject.ext.dependencies.RxAndroid

    implementation rootProject.ext.dependencies.smartRefreshKernel      //核心必须依赖
    implementation rootProject.ext.dependencies.smartRefreshFooter    //经典加载
    implementation rootProject.ext.dependencies.smartRefreshHeader    //经典刷新头

    // 圆图
    implementation rootProject.ext.dependencies.circleImage

    // 加载中、错误、正常等页面切换
    implementation  rootProject.ext.dependencies.multiStateView

    // 缓存
    implementation rootProject.ext.dependencies.diskLruCache

    // Room数据库
    implementation rootProject.ext.dependencies.roomRuntime
    kapt rootProject.ext.dependencies.roomCompiler
    implementation rootProject.ext.dependencies.roomRxJava
    
    // 点击图片查看大图
    implementation rootProject.ext.dependencies.bigImageViewPager

    implementation rootProject.ext.dependencies.mmkv

    implementation rootProject.ext.dependencies.androidPickerView
    implementation rootProject.ext.dependencies.luban
    implementation rootProject.ext.dependencies.goweiiActionBarEx

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation project(path: ':updateApp')
    implementation project(path: ':http-float-log')
    implementation project(path: ':file-download')
    implementation project(path: ':qr-code-create')
}